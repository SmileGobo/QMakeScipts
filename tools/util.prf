defineTest(LOG){
    MSG = $$1
    message($$PWD/$$pro_file():$$MSG)
    return (true)
}

defineTest(WARNING){
	MSG = $$1
	warning($$PWD/$$pro_file():$$MSG)
	return (true)
}
#общие библиотеки
defineReplace(pro_file){
    LIST = $$split(_PRO_FILE_, /)
    return ( $$last(LIST))
}

defineReplace(module_name){
    FILE = $$pro_file()
    return ($$replace(FILE, .pro, ))
}

defineReplace(module_include_path){
	PATH = $$1
	return ($$absolute_path($${DEPENDS_BASE_PATH}/$${PATH}))
}
#Функция извлекает значения хеша по ключу
#TODO название так себе
# PATH.firstModule = /path/to/first/module
# PATH.secondModule = /path/to/second/module
# for(m, MODULES){
#  INCLUDEPATH += $$MapValue(PATH, $$m)/include    
# }
defineReplace(MapValue){
    MAP = $$1
    KEY = $$2
    var = "$${MAP}.$${KEY}"
    eval(result = \$\$$${var})
    return ($$result)
}